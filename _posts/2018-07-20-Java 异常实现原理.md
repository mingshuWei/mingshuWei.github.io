---
layout:     post
title:      Java 异常
subtitle:   Java 异常简介和原理
date:       2018-07-20
author:     mingshu
catalog: true
tags:
    - Java
    - 异常
---

# 异常简介

# try catch 实现原理
## 异常表
属性表（attribute_info）可以存在于 Class 文件、字段表、方法表中，用于描述某些场景的专有信息。属性表中有个 Code 属性，该属性在方法表中使用，Java 程序方法体中的代码被编译成的字节码指令存储在 Code 属性中。而异常表（exception_table）则是存储在 Code 属性表中的一个结构，这个结构是可选的。

```
public class ExceptionClassCode {

    public int demo() {
        int x;
        try {
            x = 1;
            return x;
        } catch (Exception e) {
            x = 2;
            return x;
        } finally {
            x = 3
        }
    }
}
```
编译后的字节码
```
public int demo();
    descriptor: ()I
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=5, args_size=1
         0: iconst_1 // 生成整数1
         1: istore_1 // 将生成的整数1赋予第1号局部变量（x=1）
         2: iload_1 // 将x(=1)的值入栈
         3: istore_2 // 将栈顶的值（=1）赋予第2号变量（returnValue）
         4: iconst_3 // 生成整数3
         5: istore_1 // x=3
         6: iload_2 // returnValue=当前栈顶值（=1）
         7: ireturn // 返回returnValue（=1）
         8: astore_2 // 将Exception对象引用值赋予第2号局部变量
         9: iconst_2 // 生成整数2
        10: istore_1 // x=2
        11: iload_1 // x(=2)压入栈顶
        12: istore_3 // 将栈顶的值（=2）赋予第3号变量（returnValue）
        13: iconst_3 // 生成整数3
        14: istore_1 // x=3
        15: iload_3  // returnValue(=2)压入栈顶
        16: ireturn  // 返回returnValue（=2）
        17: astore        4 // 将异常信息保存到第4号局部变量
        19: iconst_3 // 生成整数3
        20: istore_1 // x=3
        21: aload         4 // 将异常引用值压入栈
        23: athrow // 抛出栈顶所引用的异常
      Exception table:
         from    to  target type
             0     4     8   Class java/lang/Exception # 如果0~4行字节码（try代码块）中出现Exception及其子类异常，则执行第8行（catch代码行）
             0     4    17   any # 无论0~4行字节码（try代码块）是否抛出异常，都执行第17行（finally代码行）
             8    13    17   any # 无论8~13行字节码（catch代码块）是否抛出异常，都执行第17行（finally代码行）
            17    19    17   any 
```
看到字节码中有一个Exception table（异常表）区域，这个就是与异常相关的字节码内容。它表示在from到to所指示的字节码行中，如果抛出type所对应的异常（及其子类），那么就跳到target指定的字节码行开始执行。
> https://blog.csdn.net/xialei199023/article/details/63251277
> https://blog.csdn.net/TellH/article/details/70940757